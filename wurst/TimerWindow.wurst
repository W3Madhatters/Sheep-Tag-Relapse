package TimerWindow
import TimerUtils
import ClosureTimers

public module TimerWindow
    
    static timer t
    static CallbackSingle timerCallback
    static timerdialog timerWindow
    
    static function startTimerWithWindow(real timeToWait, string title, CallbackSingle cb)
        t = getTimer()
        timerCallback = t.doAfter(timeToWait) () ->
            timerWindow.destr()
            timerWindow = null
            timerCallback = null
            cb.call()
        timerWindow = t.createTimerDialog()..setTitle(title)
        timerWindow.display(true)

    static function startTimer(real timeToWait, CallbackSingle cb)
        timerCallback = doAfter(timeToWait, cb)

    static function cancelTimer()
        if (timerCallback != null)
            destroy timerCallback
            timerCallback = null
        if (timerWindow != null)
            timerWindow.destr()
            timerWindow = null

    static function getElapsedTime() returns real
        return t.getElapsed()